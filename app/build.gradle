apply plugin: 'com.android.application'
apply from: "$rootDir/common.gradle"
apply plugin: 'io.fabric'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

apply from: "$rootDir/dependencies.gradle"

android {
    defaultConfig {
        applicationId "com.aykuttasil.sweetloc"
        versionCode 8
        versionName "1.0.7"
        multiDexEnabled true
        manifestPlaceholders = [onesignal_app_id               : "283c0725-f1ae-434a-8ea5-09f61b1246fc",
                                // Project number pulled from dashboard, local value is ignored.
                                onesignal_google_project_number: "REMOTE"]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        main {
            java.srcDir 'src/main/kotlin'
        }
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            debuggable true
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
            resValue "string", "app_name", "SweetLoc"
            resValue "string", "dynamic_links_uri_prefix", "https://sweetloc.page.link"
        }

        dev {
            dimension "default"
            resValue "string", "app_name", "SweetLoc Dev"
            resValue "string", "dynamic_links_uri_prefix", "https://sweetloc.page.link"

            versionNameSuffix '-DEV'
        }
    }

    compileOptions {
        sourceCompatibility Config.javaVersion
        targetCompatibility Config.javaVersion
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/**'
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Log
    implementation Libs.logger
    implementation Libs.timber
    implementation(Libs.crashlytics)

    // Debug
    debugImplementation Libs.stetho
    implementation Libs.stetho_okhttp3
    debugImplementation Libs.stetho_js_rhino
    debugImplementation "com.github.ChuckerTeam.Chucker:library:3.0.1"
    releaseImplementation "com.github.ChuckerTeam.Chucker:library-no-op:3.0.1"
    debugImplementation Libs.leakcanary_android

    // AndroidX
    implementation Libs.material
    implementation Libs.androidx_appcompat_appcompat
    implementation Libs.androidx_core_core_ktx
    implementation Libs.legacy_support_v4
    implementation Libs.multidex
    implementation Libs.palette
    implementation Libs.cardview
    implementation Libs.recyclerview
    implementation Libs.vectordrawable_animated
    implementation Libs.constraintlayout
    implementation Libs.fragment_ktx

    // Room
    implementation Libs.room_rxjava2
    implementation Libs.room_runtime
    kapt Libs.room_compiler

    // Navigation
    implementation Libs.navigation_fragment_ktx
    implementation Libs.navigation_ui_ktx

    // Lifecycle
    implementation Libs.lifecycle_extensions
    implementation Libs.lifecycle_reactivestreams_ktx

    // Firebase
    implementation Libs.firebase_core
    implementation Libs.firebase_messaging
    implementation Libs.firebase_database
    implementation Libs.firebase_config
    implementation Libs.firebase_auth
    implementation Libs.firebase_crash
    implementation Libs.firebase_dynamic_links
    implementation Libs.firebase_common_ktx
    implementation Libs.firebase_firestore_ktx

    // Google Play Services
    implementation Libs.play_services_base
    implementation Libs.play_services_maps
    implementation Libs.play_services_gcm
    implementation Libs.play_services_location
    implementation Libs.android_maps_utils

    // Retrofit
    implementation Libs.logging_interceptor
    implementation Libs.retrofit
    implementation Libs.converter_gson
    implementation Libs.adapter_rxjava2
    implementation Libs.adapter_rxjava

    // DI
    kapt Libs.dagger_compiler
    implementation Libs.dagger
    kapt Libs.dagger_android_processor
    implementation Libs.dagger_android_support

    // Kotlin
    implementation Libs.kotlin_stdlib_jdk8
    implementation Libs.anko
    implementation Libs.kotlinx_coroutines_core
    implementation Libs.kotlinx_coroutines_android
    implementation Libs.kotlinx_coroutines_rx2

    // Rx
    implementation Libs.rxkotlin
    implementation Libs.rxandroid
    implementation Libs.rxpermissions
    implementation Libs.rxkit
    implementation Libs.rxlocation

    // Butterknife
    implementation Libs.butterknife
    kapt Libs.butterknife_compiler

    // Glide
    implementation Libs.glide
    kapt Libs.com_github_bumptech_glide_compiler
    implementation Libs.okhttp3_integration

    // Permission
    implementation Libs.assent
    implementation Libs.needs

    implementation Libs.androidbasichelper
    implementation Libs.eventbus
    implementation Libs.facebook_android_sdk
    implementation Libs.transitionseverywhere
    implementation Libs.onesignal

    // Testing
    testImplementation Libs.junit
    testImplementation "androidx.test:core:" + versions.androidx.test_core
    testImplementation "androidx.test:runner:" + versions.androidx.test_runner
    testImplementation "androidx.test:rules:" + versions.androidx.test_rules
    testImplementation "androidx.test.ext:junit-ktx:" +versions.androidx.test_ext_junit
    testImplementation "androidx.test.ext:truth:" + versions.androidx.test_ext_truth
    testImplementation "io.mockk:mockk:" + versions.mockk

}
apply plugin: 'com.google.gms.google-services'
