apply plugin: 'com.android.application'
apply from: "$rootDir/tools/common.gradle"
apply plugin: 'io.fabric'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

frodo {
    // https://github.com/android10/frodo
    enabled = true
}

android {
    defaultConfig {
        applicationId "com.aykuttasil.sweetloc"
        versionCode 8
        versionName "1.0.7"
        multiDexEnabled true
        manifestPlaceholders = [onesignal_app_id               : "283c0725-f1ae-434a-8ea5-09f61b1246fc",
                                // Project number pulled from dashboard, local value is ignored.
                                onesignal_google_project_number: "REMOTE"]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            debuggable true
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
            resValue "string", "app_name", "SweetLoc"
            buildConfigField('String', 'BACKEND_URL', '"http://sweet-loc.herokuapp.com/"')
            resValue "string", "dynamic_links_uri_prefix", "https://sweetloc.page.link"
        }

        dev {
            dimension "default"
            resValue "string", "app_name", "SweetLoc Dev"
            buildConfigField('String', 'BACKEND_URL', '"http://sweet-loc.herokuapp.com/"')
            resValue "string", "dynamic_links_uri_prefix", "https://sweetloc.page.link"

            versionNameSuffix '-DEV'
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    compileOptions {
        sourceCompatibility Config.javaVersion
        targetCompatibility Config.javaVersion
    }

    sourceSets {
        main {
            java.srcDir 'src/main/kotlin'
        }
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/**'
    }

    androidExtensions {
        experimental = true
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation Libs.junit

    debugImplementation Libs.stetho
    implementation Libs.stetho_okhttp3
    debugImplementation Libs.stetho_js_rhino

    releaseImplementation Libs.library_no_op //'com.readystatesoftware.chuck:library-no-op:1.1.0'
    debugImplementation Libs.library //'com.readystatesoftware.chuck:library:1.1.0'

    releaseImplementation Libs.sherlock_no_op
    debugImplementation(Libs.sherlock) {
        transitive = true
    }

    // Log
    implementation Libs.logger
    implementation Libs.timber

    implementation Libs.material
    implementation Libs.legacy_support_v4
    implementation Libs.multidex
    implementation Libs.androidx_appcompat_appcompat
    implementation Libs.palette
    implementation Libs.cardview
    implementation Libs.recyclerview
    implementation Libs.vectordrawable_animated
    implementation Libs.constraintlayout

    implementation Libs.room_rxjava2
    implementation Libs.room_runtime
    kapt Libs.room_compiler

    implementation Libs.navigation_fragment_ktx
    implementation Libs.navigation_ui_ktx

    implementation Libs.lifecycle_extensions
    implementation Libs.lifecycle_reactivestreams_ktx

    implementation Libs.androidx_core_core_ktx
    implementation Libs.fragment_ktx

    implementation(Libs.crashlytics) {
        transitive = true
    }

    implementation Libs.firebase_core
    implementation Libs.firebase_messaging
    implementation Libs.firebase_database
    implementation Libs.firebase_config
    implementation Libs.firebase_auth
    implementation Libs.firebase_crash
    implementation Libs.firebase_dynamic_links
    implementation Libs.firebase_common_ktx
    implementation Libs.firebase_firestore_ktx

    implementation Libs.play_services_base
    implementation Libs.play_services_maps
    implementation Libs.play_services_gcm
    implementation Libs.play_services_location
    implementation Libs.android_maps_utils

    implementation Libs.logging_interceptor
    implementation Libs.retrofit
    implementation Libs.converter_gson
    implementation Libs.adapter_rxjava2
    implementation Libs.adapter_rxjava

    implementation Libs.picasso
    implementation Libs.androidbasichelper
    implementation Libs.eventbus
    implementation Libs.facebook_android_sdk
    implementation Libs.transitionseverywhere
    implementation Libs.rxlocation
    implementation Libs.onesignal

    kapt Libs.dagger_compiler
    implementation Libs.dagger
    kapt Libs.dagger_android_processor
    implementation Libs.dagger_android_support

    // Kotlin
    implementation Libs.kotlin_stdlib_jdk8
    implementation Libs.anko
    implementation Libs.kotlinx_coroutines_core
    implementation Libs.kotlinx_coroutines_android
    implementation Libs.kotlinx_coroutines_rx2

    implementation Libs.rxkotlin
    implementation Libs.rxandroid
    implementation Libs.rxpermissions
    implementation Libs.rxkit

    implementation Libs.butterknife
    kapt Libs.butterknife_compiler

    implementation Libs.glide
    kapt Libs.com_github_bumptech_glide_compiler
    implementation Libs.okhttp3_integration

    debugImplementation Libs.hyperion_core
    debugImplementation Libs.hyperion_attr
    debugImplementation Libs.hyperion_measurement
    debugImplementation Libs.hyperion_disk
    debugImplementation Libs.hyperion_recorder
    debugImplementation Libs.hyperion_phoenix
    debugImplementation Libs.hyperion_crash
    debugImplementation Libs.hyperion_shared_preferences
    debugImplementation Libs.hyperion_geiger_counter
    debugImplementation Libs.timber
    debugImplementation Libs.hyperion_build_config
    debugImplementation Libs.hyperion_chuck

    implementation Libs.needs

    debugImplementation Libs.leakcanary_android

    // Permission
    implementation Libs.assent


}
apply plugin: 'com.google.gms.google-services'
